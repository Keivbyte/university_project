.586P
.MODEL FLAT, stdcall

include prg31.inc

includelib d:\masm32\lib\user32.lib
includelib d:\masm32\lib\kernel32.lib

_DATA SEGMENT
	NEWHWND DD 0
	MSG MSGSTRUCT <?>
	WC WNDCLASS <?>
	HINST DD 0
	TITLENAME DB 'Пример - окно с полем редактирования', 0
	CLASSNAME DB 'CLASS32', 0
	CPBUT DB 'Выход', 0
	CPEDT DB ' ', 0
	CLSBUTN DB 'BUTTON', 0
	CLSEDIT DB 'EDIT', 0
	HWNDBTN DD 0
	HWNDEDT DD 0
	CAP DB 'Сообщение', 0
	MES DB 'Конец работы программы', 0
	TEXT DB 'Строка редактирования', 0
	DB 50 DUP(0)
_DATA ENDS
_TEXT SEGMENT
START:
PUSH 0
CALL GetModuleHandleA@4
MOV HINST, EAX
REG_CLASS:
	MOV WC.CLSSTYLE, STYLE
	MOV WC.CLWNDPROC, OFFSET WNDPROC
	MOV WC.CLSCBCLSEX, 0
	MOV WC.CLSCBWNDEX, 0
	MOV EAX, HINST
	MOV WC.CLSHINST, EAX
	PUSH IDI_APPLICATION
	PUSH 0
	CALL LoadIconA@8
	MOV WC.CLSHICON, EAX
	PUSH IDC_ARROW
	PUSH 0
	CALL LoadCursorA@8
	MOV WC.CLSHCURSOR, EAX
	MOV WC.CLBKGROUND, 17
	MOV DWORD PTR WC.CLMENNAME, 0
	MOV DWORD PTR WC.CLNAME, OFFSET CLASSNAME
	PUSH OFFSET WC
	CALL RegisterClassA@4
	PUSH 0
	PUSH HINST
	PUSH 0
	PUSH 0
	PUSH 150
	PUSH 400
	PUSH 100
	PUSH 100
	PUSH WS_OVERLAPPEDWINDOW
	PUSH OFFSET TITLENAME
	PUSH OFFSET CLASSNAME
	PUSH 0
	CALL CreateWindowExA@48
	CMP EAX, 0
	JZ _ERR
	MOV NEWHWND, EAX
	PUSH SW_SHOWNORMAL
	PUSH NEWHWND
	CALL ShowWindow@8
	PUSH NEWHWND
	CALL UpdateWindow@4
MSG_LOOP:
	PUSH 0
	PUSH 0
	PUSH 0
	PUSH OFFSET MSG
	CALL GetMessageA@16
	CMP EAX, 0
	JE END_LOOP
	PUSH OFFSET MSG
	CALL TranslateMessage@4
	PUSH OFFSET MSG
	CALL DispatchMessageA@4
	JMP MSG_LOOP
END_LOOP:
	PUSH MSG.MSWPARAM
	CALL ExitProcess@4
_ERR:
	JMP END_LOOP
	WNDPROC PROC
	PUSH EBP
	MOV EBP, ESP
	PUSH EBX
	PUSH ESI
	PUSH EDI
	CMP DWORD PTR [EBP+0CH], WM_DESTROY
	JE WMDESTROY
	CMP DWORD PTR [EBP+0CH], WM_CREATE
	JE WMCREATE
	CMP DWORD PTR [EBP+0CH], WM_COMMAND
	JE WMCOMMND
	JMP DEFWINDOWPROC
WMCOMMND:
	MOV EAX, HWNDBTN
	CMP DWORD PTR [EBP+14H], EAX
	JNE NODESTROY
	PUSH OFFSET TEXT
	PUSH 150
	PUSH WM_GETTEXT
	PUSH HWNDEDT
	CALL SendMessageA@16
	PUSH 0
	PUSH OFFSET CAP
	PUSH OFFSET TEXT
	PUSH DWORD PTR [EBP+08H]
	CALL MessageBoxA@16
	JMP WMDESTROY
NODESTROY:
	MOV EAX, 0
	JMP FINISH
WMCREATE:
	PUSH 0
	PUSH HINST
	PUSH 0
	PUSH DWORD PTR [EBP+08H]
	PUSH 20
	PUSH 60
	PUSH 10
	PUSH 10
	PUSH STYLBTN
	PUSH OFFSET CPBUT
	PUSH OFFSET CLSBUTN
	PUSH 0
	CALL CreateWindowExA@48
	MOV HWNDBTN, EAX
	PUSH 0
	PUSH HINST
	PUSH 0
	PUSH DWORD PTR [EBP+08H]
	PUSH 20
	PUSH 350
	PUSH 50
	PUSH 10
	PUSH STYLEDT
	PUSH OFFSET CPEDT
	PUSH OFFSET CLSEDIT
	PUSH 0
	CALL CreateWindowExA@48
	MOV HWNDEDT, EAX
	PUSH OFFSET TEXT
	PUSH 0
	PUSH WM_SETTEXT
	PUSH HWNDEDT
	CALL SendMessageA@16
	PUSH HWNDEDT
	CALL SetFocus@4
	MOV EAX, 0
	JMP FINISH
DEFWINDOWPROC:
	PUSH DWORD PTR [EBP+14H]
	PUSH DWORD PTR [EBP+10H]
	PUSH DWORD PTR [EBP+0CH]
	PUSH DWORD PTR [EBP+08H]
	CALL DefWindowProcA@16
	JMP FINISH
WMDESTROY:
	PUSH 0;MB_OK
	PUSH OFFSET CAP
	PUSH OFFSET MES
	PUSH DWORD PTR [EBP+08H]
	CALL MessageBoxA@16
	PUSH 0
	CALL PostQuitMessage@4
	MOV EAX, 0
FINISH:
	POP EDI
	POP ESI
	POP EBX
	POP EBP
	RET 16
WNDPROC ENDP
_TEXT ENDS
END START